plugins {
    id 'idea'
    id 'net.neoforged.gradle.userdev' version '7.0.142'
    id 'java-library'
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'

base {
    archivesName = "${mod_id}-${project.name}-${minecraft_version}"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)
java.withSourcesJar()
java.withJavadocJar()

var at = file('src/main/resources/META-INF/accesstransformer.cfg')
if (at.exists()) {
    minecraft.accessTransformers.file at
}

subsystems {
    parchment {
        minecraftVersion = parchment_minecraft_version
        mappingsVersion = parchment_version
    }
}

runs {
    configureEach {
        modSource project.sourceSets.main
    }
    client {
    }
    client_alt {
        configure('client')

        programArguments.addAll '--username', 'DevAlt'
    }
    server {
        programArgument '--nogui'
    }

    data {
        programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

jarJar.enable()

// Sets up a dependency configuration called 'localRuntime'.
// This configuration should be used instead of 'runtimeOnly' to declare
// a dependency that will be present for runtime testing but that is
// "optional", meaning it will not be pulled by dependents of this mod.
configurations {
    runtimeClasspath.extendsFrom localRuntime
}

dependencies {
    implementation "net.neoforged:neoforge:${neoforge_version}"

    jarJar implementation("dev.thomasglasser.tommylib:tommylib-neoforge-${minecraft_version}:${tommylib_version}") {
        transitive = false
    }
    jarJar implementation("maven.modrinth:midnightlib:${midnightlib_version}-neoforge")

    implementation "software.bernie.geckolib:geckolib-neoforge-${minecraft_version}:${geckolib_version}"
    implementation "net.tslat.smartbrainlib:SmartBrainLib-neoforge-1.20.5:${sbl_version}"
//    implementation "net.tslat.smartbrainlib:SmartBrainLib-neoforge-${minecraft_version}:${sbl_version}"
    implementation "top.theillusivec4.curios:curios-neoforge:${curios_version}"

    // Runtime
    localRuntime "maven.modrinth:jade:${jade_version}+neoforge"
    localRuntime "mezz.jei:jei-${previous_minecraft_version}-neoforge:${jei_version}"
}

jar {
    from(rootProject.file("LICENSE")) {
        rename { "${it}_${mod_name}" }
    }
    manifest {
        attributes([
                'Specification-Title'     : mod_name,
                'Specification-Vendor'    : mod_author,
                'Specification-Version'   : project.jar.archiveVersion,
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : mod_author,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Timestamp'               : System.currentTimeMillis(),
                'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                'Built-On-Minecraft'      : minecraft_version
        ])
    }
}

sourcesJar {
    from(rootProject.file("LICENSE")) {
        rename { "${it}_${mod_name}" }
    }
}

repositories {
    maven {
        name = 'Parchment'
        url = 'https://maven.parchmentmc.org'
        content {
            includeGroupAndSubgroups 'org.parchmentmc.data'
        }
    }
    maven {
        name = 'Fabric'
        url = 'https://maven.fabricmc.net/'
        content {
            includeGroupAndSubgroups 'net.fabricmc.fabric-api'
        }
    }
    maven {
        name = "Mod Menu"
        url 'https://maven.terraformersmc.com/releases'
        content {
            includeGroupAndSubgroups 'com.terraformersmc'
        }
    }
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
        content {
            includeGroupAndSubgroups 'maven.modrinth'
        }
    }
    maven {
        name = 'GeckoLib'
        url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
        content {
            includeGroupAndSubgroups 'software.bernie.geckolib'
            includeGroupAndSubgroups 'com.eliotlash.mclib'
        }
    }
    maven {
        name = "SmartBrainLib"
        url = "https://dl.cloudsmith.io/public/tslat/sbl/maven/"
        content {
            includeGroupAndSubgroups 'net.tslat.smartbrainlib'
        }
    }
    maven {
        name = "Illusive Soulworks maven"
        url = "https://maven.theillusivec4.top/"
        content {
            includeGroupAndSubgroups 'top.theillusivec4.curios'
        }
    }
    maven {
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/"
        content {
            includeGroupAndSubgroups 'dev.emi'
        }
    }
    maven {
        name = "Ladysnake Libs"
        url = 'https://maven.ladysnake.org/releases'
        content {
            includeGroupAndSubgroups 'dev.onyxstudios.cardinal-components-api'
        }
    }
    maven {
        name = "JEI"
        url = "https://maven.blamejared.com/"
        content {
            includeGroupAndSubgroups 'mezz.jei'
        }
    }
    maven {
        name = 'Ladysnake Mods'
        url = 'https://maven.ladysnake.org/releases'
        content {
            includeGroupAndSubgroups 'org.ladysnake.cardinal-components-api'
        }
    }

    // Non-specific
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
    maven {
        name = 'Mojang and Mixins'
        url = 'https://repo.spongepowered.org/repository/maven-public/'
    }
    mavenLocal()
}

processResources {
    var replaceProperties = [
            mod_name: mod_name,
            mod_id: mod_id,
            major_fml_version: major_fml_version,
            version: project.version,
            description: project.description,
            mod_author: mod_author,
            contributors: contributors,
            license: license,
            icon: icon,
            github_name: github_name,
            neoforge_version: neoforge_version,
            fabric_loader_version: fabric_loader_version,
            fabric_version: fabric_version,
            minecraft_version: minecraft_version,
            next_minecraft_version: next_minecraft_version,
            tommylib_version: tommylib_version
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/neoforge.mods.toml','fabric.mod.json','**/pack.mcmeta','*.mixins.json']) {
        expand replaceProperties + [project: project]
    }
}

// Disables Gradle's custom module metadata from being published to maven. The
// metadata includes mapped dependencies which are not reasonably consumable by
// other mod developers.
tasks.withType(GenerateModuleMetadata) {

    enabled = false
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId = base.archivesName.get()
            version project.version
            from components.java

            pom {
                name = project.mod_name
                description = project.description
                url = "https://github.com/thomasglasser/${github_name}/wiki"
                licenses {
                    license {
                        name = project.license
                        url = project.license_url
                    }
                }
                developers {
                    developer {
                        id = project.mod_author
                        name = project.mod_author
                    }
                }
                issueManagement {
                    system = 'GitHub'
                    url = "https://github.com/thomasglasser/${github_name}/issues"
                }
                scm
                        {
                            connection = "scm:git:git://github.com/thomasglasser/${github_name}.git"
                            developerConnection = "scm:git:ssh://github.com:thomasglasser/${github_name}.git"
                            url = "https://github.com/thomasglasser/${github_name}/tree/${minecraft_version}"
                        }
            }
        }
    }

    repositories {
        maven {
            credentials {
                username = project.findProperty("MAVEN_USER") ?: System.getenv("MAVEN_USER")
                password = project.findProperty("MAVEN_PASSWORD") ?: System.getenv("MAVEN_PASSWORD")
            }

            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications
}
